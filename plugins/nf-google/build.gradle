/*
 * Copyright 2019, Google Inc
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'io.nextflow.nextflow-plugin' version "${nextflowPluginVersion}"
    id 'java-test-fixtures'
}

nextflowPlugin {
    nextflowVersion = '25.11.0-edge'

    provider = "${nextflowPluginProvider}"
    description = 'Enables Google Cloud Platform execution through Batch service with native Cloud Storage access and intelligent machine type selection'
    className = 'nextflow.cloud.google.GoogleCloudPlugin'
    useDefaultDependencies = false
    generateSpec = false
    extensionPoints = [
        'nextflow.cloud.google.GoogleOpts',
        'nextflow.cloud.google.batch.GoogleBatchExecutor',
        'nextflow.cloud.google.util.GsPathFactory',
        'nextflow.cloud.google.util.GsPathSerializer',
    ]
}

sourceSets {
    main.java.srcDirs = []
    main.groovy.srcDirs = ['src/main']
    main.resources.srcDirs = ['src/resources']
    test.groovy.srcDirs = ['src/test']
    test.java.srcDirs = []
    test.resources.srcDirs = []
}

configurations {
    // see https://docs.gradle.org/4.1/userguide/dependency_management.html#sub:exclude_transitive_dependencies
    runtimeClasspath.exclude group: 'org.slf4j', module: 'slf4j-api'
}

dependencies {
    compileOnly project(':nextflow')
    compileOnly 'org.slf4j:slf4j-api:2.0.17'
    compileOnly 'org.pf4j:pf4j:3.12.0'

    api 'com.google.auth:google-auth-library-oauth2-http:1.39.1'
    api 'com.google.cloud:google-cloud-batch:0.75.0'
    api 'com.google.cloud:google-cloud-logging:3.23.5'
    api 'com.google.cloud:google-cloud-nio:0.128.5'
    api 'com.google.cloud:google-cloud-storage:2.58.0'

    // Force patched version to address CVE-2025-55163 (MadeYouReset HTTP/2 DDoS vulnerability)
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.75.0'

    testImplementation(testFixtures(project(":nextflow")))
    testImplementation "org.apache.groovy:groovy:4.0.29"
    testImplementation "org.apache.groovy:groovy-nio:4.0.29"
}

test {
    useJUnitPlatform()
}
