/*
 * Copyright 2021, Microsoft Corp
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'io.nextflow.nextflow-plugin' version "${nextflowPluginVersion}"
    id 'java-test-fixtures'
}

nextflowPlugin {
    nextflowVersion = '25.11.0-edge'

    provider = "${nextflowPluginProvider}"
    description = 'Enables Azure cloud execution through Batch service with native Blob storage access and comprehensive authentication options'
    className = 'nextflow.cloud.azure.AzurePlugin'
    useDefaultDependencies = false
    generateSpec = false
    extensionPoints = [
        'nextflow.cloud.azure.batch.AzBatchExecutor',
        'nextflow.cloud.azure.batch.AzBatchObserverFactory',
        'nextflow.cloud.azure.config.AzConfig',
        'nextflow.cloud.azure.file.AzPathFactory',
        'nextflow.cloud.azure.file.AzPathSerializer',
        'nextflow.cloud.azure.fusion.AzFusionEnv',
    ]
}

sourceSets {
    main.java.srcDirs = []
    main.groovy.srcDirs = ['src/main']
    main.resources.srcDirs = ['src/resources']
    test.groovy.srcDirs = ['src/test']
    test.java.srcDirs = ['src/testResources']
    test.resources.srcDirs = []
}

configurations {
    // see https://docs.gradle.org/4.1/userguide/dependency_management.html#sub:exclude_transitive_dependencies
    runtimeClasspath.exclude group: 'org.slf4j', module: 'slf4j-api'
}

dependencies {
    compileOnly project(':nextflow')
    compileOnly 'org.slf4j:slf4j-api:2.0.17'
    compileOnly 'org.pf4j:pf4j:3.12.0'
    api('com.azure:azure-storage-blob:12.31.1') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    api('com.azure:azure-compute-batch:1.0.0-beta.3') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'com.google.guava', module: 'guava'
    }
    api('com.azure:azure-identity:1.17.0') {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }

    // address security vulnerabilities
    implementation 'io.netty:netty-handler:4.1.125.Final'
    implementation 'io.netty:netty-codec-http2:4.1.125.Final'
    implementation 'net.minidev:json-smart:2.5.2'

    testImplementation(testFixtures(project(":nextflow")))
    testImplementation project(':nextflow')
    testImplementation "org.apache.groovy:groovy:4.0.30"
    testImplementation "org.apache.groovy:groovy-nio:4.0.30"
}
