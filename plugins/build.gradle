apply plugin: 'java'

ext.aws_access_key_id = project.findProperty('aws_access_key_id') ?: System.getenv('AWS_ACCESS_KEY_ID')
ext.aws_secret_access_key = project.findProperty('aws_secret_access_key') ?: System.getenv('AWS_SECRET_ACCESS_KEY')
ext.publishRepoUrl = project.findProperty('publish_repo_url') ?: System.getenv('PUBLISH_REPO_URL') ?: ( version.endsWith('-SNAPSHOT') ? "s3://maven.seqera.io/snapshots" : "s3://maven.seqera.io/releases" )

jar.enabled = false

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    group = 'io.nextflow'
    version = project.file('VERSION').text.trim()

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    /*
     * Copy the plugin dependencies in the subproject `build/target/libs` directory
     */
    task copyPluginLibs(type: Sync) {
        group 'nextflow'
        from configurations.runtimeClasspath
        into 'build/target/libs'
    }

    /*
     * publish jars maven repo on S3 
     */
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                suppressPomMetadataWarningsFor('testFixturesApiElements')
                suppressPomMetadataWarningsFor('testFixturesRuntimeElements')
            }
        }
        repositories {
            maven {
                url = publishRepoUrl
                credentials(AwsCredentials) {
                    // keys are defined in the `gradle.properties` file
                    accessKey aws_access_key_id
                    secretKey aws_secret_access_key
                }
            }
        }
    }
}

/*
 * "install" the plugin the project root build/plugins directory
 */
project.parent.tasks.getByName("assemble").dependsOn << assemble

/*
 * Copies the plugins required dependencies in the corresponding lib directory
 */
classes.dependsOn subprojects.copyPluginLibs

/*
 * Copy core plugin builds to the root build/plugins directory
 */
task copyPluginBuilds {
    group 'nextflow'
    description 'Copy core plugin builds to build/plugins'

    dependsOn subprojects.collect { it.tasks.named('assemble') }

    doLast {
        def pluginsDir = project.parent.file("${project.parent.buildDir}/plugins")
        pluginsDir.mkdirs()

        subprojects.each { subproject ->
            def pluginName = subproject.name
            def pluginVersion = subproject.version
            def pluginDir = "${pluginsDir}/${pluginName}-${pluginVersion}"

            // Copy classes
            project.copy {
                from subproject.file('build/classes/groovy/main')
                into new File("${pluginDir}/classes")
            }
            project.copy {
                from subproject.file('build/classes/main')
                into new File("${pluginDir}/classes")
            }
            project.copy {
                from subproject.file('build/resources/main/META-INF')
                into new File("${pluginDir}/classes/META-INF")
            }

            // Copy lib dependencies
            project.copy {
                from subproject.file('build/target/libs')
                into new File("${pluginDir}/lib")
            }

            // Copy MANIFEST.MF
            project.copy {
                from subproject.file('build/tmp/jar/MANIFEST.MF')
                into new File("${pluginDir}/META-INF")
            }

            println "Copied plugin: ${pluginName}-${pluginVersion}"
        }
    }
}

assemble.finalizedBy(copyPluginBuilds)
