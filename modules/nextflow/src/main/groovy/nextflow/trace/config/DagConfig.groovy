/*
 * Copyright 2024-2025, Seqera Labs
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package nextflow.trace.config

import groovy.transform.CompileStatic
import nextflow.config.schema.ConfigOption
import nextflow.config.schema.ConfigScope
import nextflow.config.schema.ScopeName
import nextflow.script.dsl.Description
import nextflow.trace.TraceHelper

@ScopeName("dag")
@Description("""
    The `dag` scope controls the workflow diagram generated by Nextflow.
""")
@CompileStatic
class DagConfig implements ConfigScope {

    @ConfigOption
    @Description("""
        When `true` enables the generation of the DAG file (default: `false`).
    """)
    final boolean enabled

    @ConfigOption
    @Description("""
        *Supported by the HTML and Mermaid renderers.*

        Controls the maximum depth at which to render sub-workflows (default: no limit).
    """)
    final int depth

    @ConfigOption
    @Description("""
        *Supported by the Graphviz, DOT, HTML and Mermaid renderers.*

        Controls the direction of the DAG, can be `'LR'` (left-to-right) or `'TB'` (top-to-bottom) (default: `'TB'`).
    """)
    final String direction

    @ConfigOption
    @Description("""
        Graph file name (default: `'dag-<timestamp>.html'`).
    """)
    final String file

    @ConfigOption
    @Description("""
        When `true` overwrites any existing DAG file with the same name (default: `false`).
    """)
    final boolean overwrite

    @ConfigOption
    @Description("""
        *Only supported by the HTML and Mermaid renderers.*

        When `false`, channel names are omitted, operators are collapsed, and empty workflow inputs are removed (default: `false`).
    """)
    final boolean verbose

    /* required by extension point -- do not remove */
    DagConfig() {}

    DagConfig(Map opts) {
        enabled = opts.enabled as boolean
        depth = opts.depth != null ? opts.depth as int : -1
        direction = opts.direction ?: 'TB'
        file = opts.file ?: defaultFileName()
        overwrite = opts.overwrite as boolean
        verbose = opts.verbose as boolean
    }

    static final String defaultFileName() {
        return "dag-${TraceHelper.launchTimestampFmt()}.html"
    }

}
