plugins {
    id "com.gradleup.shadow" version "8.3.5"
}
apply plugin: 'groovy'
apply plugin: 'application'

sourceSets {
    main.java.srcDirs = []
    main.groovy.srcDirs = ['src/main/java', 'src/main/groovy']
    main.resources.srcDirs = ['src/main/resources']
    test.java.srcDirs = []
    test.groovy.srcDirs = ['src/test/groovy']
    test.resources.srcDirs = ['src/test/resources']
}

compileGroovy {
    options.compilerArgs = ['-XDignore.symbol.file']
}

dependencies {
    api(project(':nf-commons'))
    api(project(':nf-httpfs'))
    api "org.apache.groovy:groovy:4.0.24"
    api "org.apache.groovy:groovy-nio:4.0.24"
    api "org.apache.groovy:groovy-xml:4.0.24"
    api "org.apache.groovy:groovy-json:4.0.24"
    api "org.apache.groovy:groovy-templates:4.0.24"
    api "org.apache.groovy:groovy-yaml:4.0.24"
    api "org.slf4j:jcl-over-slf4j:2.0.16"
    api "org.slf4j:jul-to-slf4j:2.0.16"
    api "org.slf4j:log4j-over-slf4j:2.0.16"
    api "ch.qos.logback:logback-classic:1.5.13"
    api "ch.qos.logback:logback-core:1.5.13"
    api "org.codehaus.gpars:gpars:1.2.1"
    api("ch.artecat.grengine:grengine:3.0.2") { exclude group: 'org.codehaus.groovy' }
    api "commons-lang:commons-lang:2.6"
    api "commons-codec:commons-codec:1.15"
    api "commons-io:commons-io:2.15.1"
    api "com.beust:jcommander:1.35"
    api("com.esotericsoftware.kryo:kryo:2.24.0") { exclude group: 'com.esotericsoftware.minlog', module: 'minlog' }
    api('org.iq80.leveldb:leveldb:0.12')
    api('org.eclipse.jgit:org.eclipse.jgit:7.1.0.202411261347-r')
    api ('javax.activation:activation:1.1.1')
    api ('javax.mail:mail:1.4.7')
    api ('org.yaml:snakeyaml:2.2')
    api ('org.jsoup:jsoup:1.15.4')
    api 'jline:jline:2.9'
    api 'org.pf4j:pf4j:3.12.0'
    api 'dev.failsafe:failsafe:3.1.0'
    api 'org.bouncycastle:bcprov-ext-jdk18on:1.78.1'
    api 'org.bouncycastle:bcpkix-jdk18on:1.78.1'

    testImplementation 'org.subethamail:subethasmtp:3.1.7'

    // test configuration
    testFixturesApi ("org.apache.groovy:groovy-test:4.0.24") { exclude group: 'org.apache.groovy' }
    testFixturesApi ("org.objenesis:objenesis:3.4")
    testFixturesApi ("net.bytebuddy:byte-buddy:1.14.17")
    testFixturesApi ("org.spockframework:spock-core:2.3-groovy-4.0") { exclude group: 'org.apache.groovy' }
    testFixturesApi ('org.spockframework:spock-junit4:2.3-groovy-4.0') { exclude group: 'org.apache.groovy' }
    testFixturesApi 'com.google.jimfs:jimfs:1.2'

}


test {
    minHeapSize = "512m"
    maxHeapSize = "4096m"
}

application {
    mainClass = 'nextflow.cli.Launcher'
}

run{
    args( (project.hasProperty("runCmd") ? project.findProperty("runCmd") : "set a cmd to run").split(' ') )
}

shadowJar {
    archiveClassifier='one'
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
    mergeServiceFiles()
    mergeGroovyExtensionModules()
}
